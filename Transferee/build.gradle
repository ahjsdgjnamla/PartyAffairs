apply plugin: 'com.android.library'
// JitPack Maven
apply plugin: 'com.github.dcendents.android-maven'
// Group
group='com.github.hitomi'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'


    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 28
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

// 指定编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// 打包源码
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// 制作文档(Javadoc)
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {
    implementation rootProject.ext.libSupportV7
    //noinspection GradleCompatible
    implementation 'com.android.support:recyclerview-v7:28+'
    implementation 'com.github.filippudak.progresspieview:library:1.0.4'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
}
